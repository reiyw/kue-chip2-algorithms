*** KUE-CHIP2 Assembler ver.1.0   by H.Ochi ***

* Calculate Least Common Multiple using Euclidean Algorithm
* Programmed by Ryo Takahashi, April. 25, 2014
* Last modified: May 9, 2014

* A (0 < A < 128)
 80 :           A:      EQU          80H
* B (0 < B < A < 128)
 81 :           B:      EQU          81H
* GCD
 82 :           GCD:    EQU          82H
* LCM (Result)
 83 :           LCM:    EQU          83H

 00 :   64 80           LD    ACC,   [A]
 02 :   6C 81           LD    IX,    [B]
 04 :   A1      LP1:    SUB   ACC,   IX
 05 :   32 04           BZP          LP1
 07 :   B1              ADD   ACC,   IX
 08 :   C8              EOR   IX,    ACC
 09 :   C1              EOR   ACC,   IX
 0A :   C8              EOR   IX,    ACC
 0B :   31 04           BNZ          LP1
 0D :   74 82           ST    ACC,   [GCD]
 0F :   C0              EOR   ACC,   ACC
 10 :   74 83           ST    ACC,   [LCM]     ; ACCを0にしたついでにLCMも0にしておく．
 12 :   74 84           ST    ACC,   [LCM+1]   ; LCMは2バイト必要．
 14 :   B4 82   LP2:    ADD   ACC,   [GCD]     ; IX = B div GCD とするためのループ．
 16 :   BA 01           ADD   IX,    1
 18 :   F4 81           CMP   ACC,   [B]
 1A :   31 14           BNZ          LP2
 1C :   20      LP3:    RCF                    ; A * IX をやりたいのだが，1バイトの
 1D :   64 80           LD    ACC,   [A]       ; 乗算プログラムをここに持ってくると
 1F :   94 84           ADC   ACC,   [LCM+1]   ; かなりの大きさになることが想定されるので
 21 :   74 84           ST    ACC,   [LCM+1]   ; IX回だけAを繰り返し足すことにした．
 23 :   35 2B           BNC          SKIP      ; IXが大きい時はかなり低速になる．
 25 :   64 83           LD    ACC,   [LCM]
 27 :   B2 01           ADD   ACC,   1
 29 :   74 83           ST    ACC,   [LCM]
 2B :   AA 01   SKIP:   SUB   IX,    1
 2D :   31 1C           BNZ          LP3
 2F :   0F              HLT

                        END
